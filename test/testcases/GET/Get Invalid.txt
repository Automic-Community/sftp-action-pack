*** Settings ***
Suite Setup       Suite Setup
Force Tags        sftp get    invalid    win    unix
Test Template     Get Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    Host                     Port        Source                       User        Password        Timeout    Message
Wrong Host            vvnappserver01invalid    \           ${_WORKING_DIR}${/}source    automic     automic         \          ${SessionCannotBeOpened}

Wrong Port            vvnappserver01           13999999    ${_WORKING_DIR}${/}source    automic     automic         \          ${SessionCannotBeOpened}

Wrong User            vvnappserver01           \           ${_WORKING_DIR}${/}source    automic1    automic         \          ${EMPTY}

Wrong Password        vvnappserver01           \           ${_WORKING_DIR}${/}source    root        ventum2011@1    10         ${EMPTY}

Anonymous User        vvnappserver01           \           ${_WORKING_DIR}${/}source    \           \               5          ${SessionCannotBeOpened}

*** Keywords ***
Suite Setup
    Init AE
    Init SFTP Server

Get Template
    [Arguments]    ${host}    ${port}    ${source}    ${user}    ${password}    ${timeout}
    ...    ${message}
    Init Action    ${ACTION_GET}
    Action Set    ${PRT_SFTP_HOST}    ${host}
    Action Set    ${PRT_SFTP_PORT}    ${port}
    Action Set    ${PRT_SFTP_SOURCE_FILES}    ${source}
    Action Set    ${PRT_SFTP_USER}    ${user}
    Action Set    ${PRT_SFTP_PWD}    ${password}
    Action Set    ${PRT_SFTP_RECURSIVE_GET}    YES
    Action Set    ${PRT_SFTP_CONNECT_TIMEOUT}    ${timeout}
    Action Set    ${PRT_SFTP_TGT_FILES_DIR}    ${_TARGET_DIR}${/}target
    Action Set    ${PRT_SFTP_TGT_OVERWRITE}    YES
    Action Set    ${PRT_SFTP_PRESERVE_ATT}    YES
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Not Equal    0
    Action Return Status Should Be    ${EndedNotOK}
    Action Report Should Contain    ${message}
    Action Report Should Contain    START AT
    Action Report Should Contain    Return Code: 999
